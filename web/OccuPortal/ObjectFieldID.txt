/**
 * =====================================
 * ObjectFieldID ( field ; file )
 *
 * RETURNS:
 *		FileMaker's internal ID for a field.
 *
 * PARAMETERS:
 *		field: The field (not literal field name) to retrieve the ID of.
 *		file: The name of the FileMaker file containing the field.
 *
 * DEPENDENCIES: none
 *
 * NOTE:
 *		If there is a layout in the file with the same name as the field's
 *		table, the field must be on that layout for this function to work.
 *
 * HISTORY:
 *		MODIFIED on 2012-01-26 by Jeremy Bante to accept the file parameter.
 *		CREATED on 2011-06-01 by Jeremy Bante <http://scr.im/jbante>.
 *		INSPIRED by custom functions by Fabrice Nordman.
 * =====================================
 */

Let ( [
	~fieldName = Substitute ( GetFieldName ( field ) ; "::" ; "¶" );
	~tableName = GetValue ( ~fieldName ; 1 );
	~fieldName = GetValue ( ~fieldName ; 2 );

	~nameList = ¶ & FieldNames ( file ; ~tableName ) & ¶;
	~value =
		Position ( ~nameList ; ¶ & ~fieldName & ¶ ; 1 ; 1 );
	~value = PatternCount ( Left ( ~nameList ; ~value ) ; "¶" )
];
	GetValue ( FieldIDs ( file ; ~tableName ) ; ~value )
)