/*
AntiFilterValues( ListA; ListB)
Finds values that are only contained in ListA

Example:

AntiFilterValues( "1¶2¶3¶4¶1¶"; "1¶3")
// "2¶4¶"

AntiFilter( ProductIDListFor_This_MonthOrders; ProductIDListFor_Last_MonthOrders)
// products ordered for first time

Bruce Robertson/Concise Design 
Updated 10/13/2008 - now correctly fully matches list item
Updated 5/3/2009 - correct results if listB is empty (returns ListA)
*/

Case( 
IsEmpty( ListA); "" ; 
IsEmpty( ListB & $top ); ListA;

ValueCount( ListB); 
Let( [
VCA = ValueCount( ListA);
VCB = ValueCount( ListB);
$Top = "##END##¶" ;
ListA = LeftValues( ListB; VCB) & $Top & LeftValues( ListA; VCA) ];
AntiFilterValues( ListA; "" )
);

Let([
VCA = ValueCount( ListA);
P = Position( ListA; $top; 0 ; 1 );
P  = PatternCount( Left( ListA; P); "¶");

L0 = MiddleValues( ListA; 1; 1);
L1 = ¶ & MiddleValues( ListA; 1; 1);
L2 = ¶ & MiddleValues( ListA; 2; 1);
L3 = ¶ & MiddleValues( ListA; 3; 1);
L4 = ¶ & MiddleValues( ListA; 4; 1);
L5 = ¶ & MiddleValues( ListA; 5; 1);

L6 = ¶ & MiddleValues( ListA; 6; 1);
L7 = ¶ & MiddleValues( ListA; 7; 1);
L8 = ¶ & MiddleValues( ListA; 8; 1);
L9 = ¶ & MiddleValues( ListA; 9; 1);
L10=¶ & MiddleValues( ListA; 10; 1);

L11 = ¶ & MiddleValues( ListA; 11; 1);
L12 = ¶ & MiddleValues( ListA; 12; 1);
L13 = ¶ & MiddleValues( ListA; 13; 1);
L14 = ¶ & MiddleValues( ListA; 14; 1);
L15 = ¶ & MiddleValues( ListA; 15; 1);

L16 = ¶ & MiddleValues( ListA; 16; 1);
L17 = ¶ & MiddleValues( ListA; 17; 1);
L18 = ¶ & MiddleValues( ListA; 18; 1);
L19 = ¶ & MiddleValues( ListA; 19; 1);
L20 = ¶ & MiddleValues( ListA; 20; 1)
];

Case( 
(L0 <> $Top) and (P > 20);
Let( R = Substitute( ¶ & ListA; // [¶ ; "¶¶"];
[L1; ¶ ]; [L2; ¶ ]; [L3; ¶ ]; [L4; ¶ ]; [L5; ¶ ]; 
[L6; ¶ ]; [L7; ¶ ]; [L8; ¶ ]; [L9; ¶ ]; [L10; ¶ ];
[L11; ¶ ]; [L12; ¶ ]; [L13; ¶ ]; [L14; ¶ ]; [L15; ¶ ]; 
[L16; ¶ ]; [L17; ¶]; [L18; ¶ ]; [L19; ¶ ]; [L20; ¶ ];["¶¶"; ¶]
); 
AntiFilterValues( MiddleValues( R; 2; ValueCount( R)-1) ;  "") ) ; 

L0 <> $Top ;
Let( R = Substitute( ¶ & ListA;  [¶; "¶¶"]; [ L1; ¶ ]; ["¶¶"; ¶] ); 
AntiFilterValues( MiddleValues( R; 2; ValueCount(R) -1);  "") ) ; 

MiddleValues( Substitute( ListA; ["¶¶"; ¶] ) ; 2; VCA-1 ) & Let ($top = ""; "")
)
)
)