/**
 * =====================================
 * ObjectLayoutID ( name ; file )
 *
 * RETURNS:
 *		FileMaker's internal ID for a layout.
 *
 * PARAMETERS:
 *		name: The name of the layout
 *		file: The name of the FileMaker file containing the layout.
 *
 * DEPENDENCIES: none
 *
 * HISTORY:
 *		MODIFIED on 2012-01-26 by Jeremy Bante to accept the file parameter.
 *		CREATED on 2011-06-01 by Jeremy Bante <http://scr.im/jbante>.
 *		INSPIRED by custom functions by Fabrice Nordman.
 * =====================================
 */

Let ( [
	~nameList = ¶ & LayoutNames ( file ) & ¶;
	~value =
		Position ( ~nameList ; ¶ & name & ¶ ; 1 ; 1 );
	~value = PatternCount ( Left ( ~nameList ; ~value ) ; ¶ )
];
	GetValue ( LayoutIDs ( file ) ; ~value )
)